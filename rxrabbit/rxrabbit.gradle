import static java.lang.System.getenv

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: "maven-publish"
apply plugin: 'com.jfrog.bintray'

dependencies {

    compile 'com.rabbitmq:amqp-client:4.3.0'
    compile 'io.reactivex:rxjava:1.3.4'
    compile 'com.google.guava:guava:23.4-jre'
    compile "org.slf4j:slf4j-api:1.7.25"

    testCompile project(':test-utils')
    testCompile 'ch.qos.logback:logback-classic:1.2.3'
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
}


task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}


def pomConfig = {
    licenses {
        license {
            name 'MIT License'
            url 'http://www.opensource.org/licenses/mit-license.php'
            distribution 'repo'
        }
    }

    scm {
        url 'https://github.com/meltwater/rxrabbit'
        connection 'scm:git:git://github.com/meltwater/rxrabbit.git'
        developerConnection 'scm:git:git@github.com:meltwater/rxrabbit.git'
    }

    developers {
        developer {
            name 'Meltwater Team Horace'
            email 'horace@meltwater.com'
            id 'meltwater/horace'
        }
    }
}


publishing {
    publications {
        JavaArtifacts(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar
            groupId project.group
            artifactId project.name
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'RxRabbit is a resilient ReactiveX styled RabbitMQ java 8 client library')
                root.appendNode('name', project.name)
                root.appendNode('url', 'https://github.com/meltwater/rxrabbit')
                root.children().last() + pomConfig
            }
        }
    }
    repositories {
        maven {
            credentials {
                username getenv('MAVEN_USER')
                password getenv('MAVEN_PASSWORD')
            }
            url getenv('MAVEN_URL')
        }
    }
}


bintray {
    user = getenv('BINTRAY_USER')
    key = getenv('BINTRAY_KEY')
    publications = ['JavaArtifacts']
    publish = true
    pkg {
        repo = 'opensource'
        name = project.name
        userOrg = 'meltwater'
        licenses = ['MIT']
        vcsUrl = "https://github.com/meltwater/${project.name}"
        version {
            gpg{
                sign = true
            }
            released  = new Date()
        }
    }
}
