import static java.lang.System.getenv

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: "maven-publish"
apply plugin: 'com.jfrog.bintray'

dependencies {

    compile 'com.rabbitmq:amqp-client:3.5.6' //TODO upgrade to 3.6.5 (or later) upgrading failes the multi broker test :-(
    compile 'io.reactivex:rxjava:1.2.0'
    compile 'com.google.guava:guava:18.0'  //TODO upgrade to 19.0 ??
    compile "org.slf4j:slf4j-api:1.7.12"

    testCompile project(':test-utils')
    testCompile 'ch.qos.logback:logback-classic:1.1.7'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
}


task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}


publishing {
    publications {
        JavaArtifacts(MavenPublication) {
            from components.java
            artifact sourceJar
            groupId project.group
            artifactId project.name
            version project.version
        }
    }
    repositories {
        maven {
            credentials {
                username getenv('MAVEN_USER')
                password getenv('MAVEN_PASSWORD')
            }
            url getenv('MAVEN_URL')
        }
    }
}

bintray {
    user = getenv('BINTRAY_USER')
    key = getenv('BINTRAY_KEY')
    publications = ['JavaArtifacts']
    publish = true
    pkg {
        repo = 'opensource'
        name = project.name
        userOrg = 'meltwater'
        licenses = ['MIT']
        vcsUrl = "https://github.com/meltwater/${project.name}"
        version {
            gpg{
                sign = true
            }
            released  = new Date()
        }
    }
}